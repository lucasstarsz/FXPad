plugins {
    id 'org.openjfx.javafxplugin' version '0.0.9'
    id 'org.beryx.jlink' version '2.23.1'
}

application {
    mainModule.set('formatfx')
    mainClass.set('org.lucasstarsz.fxpad.RTFXMain')
}

repositories {
    mavenCentral()
}

dependencies {
    // https://mvnrepository.com/artifact/org.fxmisc.richtext/richtextfx
    implementation 'org.fxmisc.richtext:richtextfx:0.10.5'
}

javafx {
    version = '14'
    modules = ['javafx.controls', 'javafx.fxml']
}

project.ext.buildDate = new Date()
project.version = '0.2.0'
project.description = "A simple, clean text editor. SNAPSHOT BUILD - Now runs using RichTextFX"

def usingSnapshot = true
def snapshotVersion = "SNAPSHOT.3"

jlink {
    addExtraDependencies("javafx")
    options.addAll('--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages')
    launcher {
        name = "FXPad-$project.version" + (usingSnapshot ? "-$snapshotVersion" : "")
        noConsole = true
    }
    jpackage {
        jvmArgs += ['-XX:+CreateCoredumpOnCrash']
        installerOptions += [
                '--description', project.description,
                '--vendor', 'Andrew Dey',
                '--copyright', 'CopyrightÂ© 2020 Andrew Dey',
                '--license-file', 'src/main/resources/LICENSE',
                '--file-associations', 'src/main/resources/extensions/extension_txt.properties',
                '--file-associations', 'src/main/resources/extensions/extension_text.properties',
                '--app-version', project.version
        ]

        def currentOs = org.gradle.internal.os.OperatingSystem.current()
        def imgType = currentOs.windows ? 'ico' : currentOs.macOsX ? 'icns' : 'png'
        icon = "src/main/resources/icons/fxpad_$imgType.$imgType"
        if (currentOs.windows) {
            installerType = "msi"
            installerOptions += [
                    '--win-per-user-install',
                    '--install-dir', 'FXPad Test',
                    '--win-dir-chooser',
                    '--win-shortcut',
                    '--win-menu', '--win-menu-group', 'Productivity'
            ]
        } else if (currentOs.linux) {
            installerType = "deb"
            installerOptions += [
                    '--linux-menu-group', 'Office',
                    '--linux-shortcut',
                    '--linux-deb-maintainer', 'andrewrcdey@gmail.com'
            ]
        }
    }
}
